import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

buildscript {
    ext {
        today = (new SimpleDateFormat("yyyyMMdd").format(new Date()))
    }
}

android {
    compileSdkVersion 32

    buildFeatures {
        dataBinding true
    }
    
    defaultConfig {
        applicationId "kr.hyosang.app.drivediary"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        resValue "string", "build_date", today

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

preBuild.doLast {
        tasks.each { task ->
            def m = task.name =~ /assemble(.*Debug)/
            if (m.matches()) {
                task.doLast({
                    println "Copying apk to bin directory ${m.group(1)}"
                    android.applicationVariants.all { variant ->
                        if (variant.name.compareToIgnoreCase(m.group(1)) == 0) {
                            println "Copy ${variant.outputs[0].outputFile}"
                            copy {
                                from variant.outputs[0].outputFile
                                into(this.rootDir.getAbsolutePath() + "/bin")
                                rename("app-debug.apk", this.rootProject.name + ".apk")
                            }
                        }
                    }
                })
            }
        }
}

clean.doLast {
        File b = new File(rootDir.getAbsolutePath() + "/bin")
        files { b.listFiles() }.each {
            println "Delete ${it}"
            delete(it)
        }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
